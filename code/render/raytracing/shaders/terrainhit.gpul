//------------------------------------------------------------------------------
//  @file terrainhit.gpul
//  @copyright (C) 2026 Individual contributors, see AUTHORS file
//------------------------------------------------------------------------------
#include <lib/raytracing.gpuh>

//------------------------------------------------------------------------------
/**
*/
entry_point
BoxHit(
    in ray_payload Result : LightResponsePayload,
    ray_hit_attribute baryCoords : f32x2
) void
{
    const obj = bufferLoad(TlasInstanceBuffer, tlasGetInstanceCustomIndex());
    const barycentricCoords = f32x3(1.0f - baryCoords.x - baryCoords.y, baryCoords.x, baryCoords.y);

    var indices : u32x3;
    var uv : f32x2;
    var normal, tangent : f32x3;
    var sign : f32;
	var tbn : f32x3x3;
    const mat = MaterialPointers.TerrainMaterials[obj.MaterialOffset];
    SampleTerrain(obj, blasGetPrimitiveIndex(), barycentricCoords, &indices, &uv, &tbn);

    const normals = sample2DLod(mat.LowresNormalFallback, NormalSampler, uv, 0);
    const tNormal = TangentSpaceNormal(normals.xy, tbn);

    const albedo = sample2DLod(mat.LowresAlbedoFallback, Basic2DSampler, uv, 0);
    const material = sample2DLod(mat.LowresMaterialFallback, Basic2DSampler, uv, 0);
    
    const worldPos = rayGetWorldOrigin() + rayGetWorldDirection() * rayGetHitDistance();
    const radiance = CalculateLightRT(worldPos, rayGetWorldOrigin().xyz, rayGetHitDistance() / rayGetTMax(), albedo.rgb, material, tNormal) + albedo.xyz * material[MAT_EMISSIVE] * albedo.a;
        
    Result.alpha = albedo.a;
    Result.albedo = albedo.rgb;
    Result.radiance = radiance;
    Result.normal = tNormal;
    Result.material = material;
    Result.depth = rayGetHitDistance();
}

//------------------------------------------------------------------------------
/**
*/
@Mask("Hit")
program Main
{
    RayClosestHitShader = BoxHit;
};
